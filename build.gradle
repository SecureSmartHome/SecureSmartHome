/*
 * MIT License
 *
 * Copyright (c) 2016.
 * Bucher Andreas, Fink Simon Dominik, Fraedrich Christoph, Popp Wolfgang,
 * Sell Leon, Werli Philemon
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.0.0'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.8'
}

ext {
    sign = false
}

allprojects {
    if (project == rootProject) return

    group = "de.unipassau.isl.evs.ssh"
    version = "0.1"

    repositories {
        jcenter()
        maven {
            url "https://dl.bintray.com/n-coder/maven/"
        }
    }

    if (project.name == "drivers" || project.name == "core") {
        apply plugin: 'com.android.library'
    } else {
        apply plugin: 'com.android.application'
    }

    android {
        compileSdkVersion 22
        buildToolsVersion "22.0.1"

        defaultConfig {
            minSdkVersion 19
            targetSdkVersion 22
            versionCode 1
            versionName "1.0"

            testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

            multiDexEnabled true
        }
        buildTypes {
            release {
                minifyEnabled false
                proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            }
        }
        if (sign) {
            signingConfigs {
                release {
                    storeFile file("evs-ssh.jks")
                    storePassword System.getenv("SIGN_STORE_PWD")
                }
            }
            buildTypes {
                release {
                    signingConfig signingConfigs.release
                }
            }
        }
        packagingOptions {
            exclude 'META-INF/LICENSE'
            exclude 'META-INF/NOTICE'
            exclude 'META-INF/io.netty.versions.properties'
            exclude 'META-INF/INDEX.LIST'
        }
        lintOptions {
            abortOnError false
        }
        configurations.all {
            resolutionStrategy {
                force 'com.android.support:support-annotations:22.2.1'
            }
        }
        testOptions {
            unitTests.returnDefaultValues = true
        }
    }

    dependencies {
        androidTestCompile 'com.android.support.test:runner:0.4'
        androidTestCompile 'com.android.support.test:rules:0.4'
        androidTestCompile 'com.android.support:support-annotations:22.2.1'
        androidTestCompile 'org.hamcrest:hamcrest-library:1.3'
        testCompile 'junit:junit:4.12'
        testCompile "org.mockito:mockito-core:1.9.5"

        compile 'com.android.support:multidex:1.0.0'
    }
}